// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Para pre establecer X
    Not16(in= x, out= notx);
    Mux4Way16(a= x, b= notx, c[0..15]= false, d[0..15]= true, sel[1]= zx, sel[0]= nx, out= a);

    // Para pre establecer y
    Not16(in= y, out= noty);
    Mux4Way16(a= y, b= noty, c[0..15]= false, d[0..15]= true, sel[1]= zy, sel[0]= ny, out= b);

    //Out
    Add16(a = a, b = b, out = aAddb);
    And16(a= a, b= b, out= aAndb);
    Mux16(a= aAndb, b= aAddb, sel= f, out= outAux);

    Not16(in= outAux, out= notOut);
    Mux16(a= outAux, b= notOut, sel= no, out= out, out[15]= ng, out[0..7]= LSB, out[8..15] = MSB); // aprovechamos el MSB para establecer ng

    // zr
    Or8Way(in= LSB, out= orlsb);
    Or8Way(in= MSB, out= ormsb);
    Or(a= orlsb, b= ormsb, out= lsbOrmsb);
    Not(in= lsbOrmsb, out= zr);
}